apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName='example.Main'

repositories {
    mavenLocal()
    jcenter()
}

ext.jmeVersion = "[3.1,)" 

project(":assets") {
    apply plugin: "java"
    
    buildDir = rootProject.file("build/assets")
    
    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }    
}


dependencies {
 
    // Need at least basic JME
    compile "org.jmonkeyengine:jme3-core:$jmeVersion"
    compile "org.jmonkeyengine:jme3-desktop:$jmeVersion"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmeVersion"
 
    // We definitely want a UI
    //compile "com.simsilica:lemur:[1.8,)"
    compile "com.simsilica:lemur:1.9.1-SNAPSHOT"
    //compile "com.simsilica:lemur-proto:[1.6,)"
    compile "com.simsilica:lemur-proto:1.7.2-SNAPSHOT"

    // And our base code for sim-etheral, SiO2, etc.
    // (many of these need to be built locally and 'gradle install'ed as of this writing)
    compile "com.simsilica:sio2:1.0.3-SNAPSHOT"
    compile "com.simsilica:sim-math:[1.0,)"
    //compile "com.simsilica:sim-ethereal:[1.1,)"    
    compile "com.simsilica:sim-ethereal:1.2.1-SNAPSHOT"

    // Standard utility stuff
    compile 'com.google.guava:guava:19.0'
    compile 'org.slf4j:slf4j-api:1.7.13'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    
    runtime project(':assets')    
}

task wrapper(type: Wrapper) {
}

/*
This actually exits immediately since there is no standard input stream
task runServer(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "example.net.server.GameServer"
    jvmArgs '-Dlog4j.configurationFile=server-log4j2.xml'
}
*/

// Create a custom server start script in the distribution
task serverStartScript(type: CreateStartScripts) {
    mainClassName = "example.net.server.GameServer"
    applicationName = "server"
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
    defaultJvmOpts = ['-Dlog4j.configurationFile=server-log4j2.xml']
}

/*applicationDistribution.into("bin") {
    from(serverStartScript)
    fileMode = 0755
}*/

// I think this is ultimately clearer than the above
distributions {
    main {
        contents {
            from(serverStartScript) {
                into "bin"
            }
        }
    }
}

// Either way we end up with dupes if we don't do this
distZip {
    duplicatesStrategy = 'exclude'
}
